; Example PICA200 vertex shader

; Uniforms
.fvec projection[4], model[4], view[4]

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.5)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones
.alias  half  myconst.wwww ; Vector full of 0.5

; Outputs
.out outpos position
.out outtc0 texcoord0
.out outtc1 texcoord1
.out outclr color
.out outview view
.out outnq normalquat

; Inputs (defined as aliases for convenience)
.in inpos v0
.alias intex v1
.alias innrm v2

.proc main
	; Force the w component of inpos to be 1.0
	mov r0.xyz, inpos
	mov r0.w,   ones

	; r1 = model * inpos
	dp4 r1.x, model[0], r0
	dp4 r1.y, model[1], r0
	dp4 r1.z, model[2], r0
	dp4 r1.w, model[3], r0

	; outview = -r1
	mov outview, -r1

	; outpos = projection * r1
	dp4 outpos.x, projection[0], r1
	dp4 outpos.y, projection[1], r1
	dp4 outpos.z, projection[2], r1
	dp4 outpos.w, projection[3], r1

	; outtex = intex
	mov outtc0, intex
	mov outtc1, intex

	; Transform the normal vector with the model matrix
	; TODO: use a separate normal matrix that is the transpose of the inverse of model
	dp3 r14.x, model[0], innrm
	dp3 r14.y, model[1], innrm
	dp3 r14.z, model[2], innrm
	dp3 r6.x, r14, r14
	rsq r6.x, r6.x
	mul r14.xyz, r14.xyz, r6.x

	mov r0, myconst.yxxx
	add r4, ones, r14.z
	mul r4, half, r4
	cmp zeros, ge, ge, r4.x
	rsq r4, r4.x
	mul r5, half, r14
	jmpc cmp.x, degenerate

	rcp r0.z, r4.x
	mul r0.xy, r5, r4

degenerate:
	mov outnq, r0
	mov outclr, ones

	; We're finished
	end
.end